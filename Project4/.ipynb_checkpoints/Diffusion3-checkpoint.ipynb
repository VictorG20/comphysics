{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 78,
   "id": "ffb588e8",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "heat_cn (generic function with 1 method)"
      ]
     },
     "execution_count": 78,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "using BenchmarkTools\n",
    "using Plots\n",
    "using LinearAlgebra\n",
    "\n",
    "function heat_ftcs(initial_T::Function, Δx::Float64, Δt::Float64, nt::Int, L, K, C, ρ)    \n",
    "    nx = trunc(Int, L/Δx)\n",
    "    T = zeros(nx-1, nt+1)\n",
    "    T[:,1] = [initial_temperature(x, L) for x in range(Δx, step=Δx, length=nx-1)]\n",
    "    \n",
    "    a = (Δt*K)/(C*ρ*Δx^2)\n",
    "    \n",
    "    for j in range(2, nt+1)\n",
    "        T[1, j] = (1-2*a)* T[1, j-1] + a*T[2, j-1]\n",
    "        for i in range(2, nx-2)\n",
    "            T[i, j] = (1-2*a)* T[i, j-1] + a*(T[i+1, j-1] + T[i-1,j-1])\n",
    "        end\n",
    "        T[end, j] = (1-2*a)* T[end, j-1] + a*T[end-1, j-1]\n",
    "    end\n",
    "    \n",
    "    return T\n",
    "end\n",
    "\n",
    "\n",
    "function heat_dufofra(initial_T::Function, Δx::Float64, Δt::Float64, nt::Int, L, K, C, ρ)\n",
    "    nx = trunc(Int, L/Δx)\n",
    "    T = zeros(nx-1, 3)\n",
    "    T[:, 1] = [initial_temperature(x, L) for x in range(Δx, step=Δx, length=nx-1)]\n",
    "    \n",
    "    a = (2*Δt*K)/(C*ρ*Δx^2)\n",
    "    \n",
    "    c1 = (1. - a)/(1. + a)\n",
    "    c2 = a/(1. + a)\n",
    "    \n",
    "    # First step is done with an FTCS scheme\n",
    "    T[1, 2] = (1. - a)* T[1, 1] + (a/2.)*T[2, 1]\n",
    "    for i in range(2, nx-2)\n",
    "        T[i, 2] = (1. - a)* T[i, 1] + (a/2.)*(T[i+1, 1] + T[i-1,1])\n",
    "    end\n",
    "    T[end, 2] = (1. - a)* T[end, 1] + (a/2.)*T[end-1, 1]\n",
    "    \n",
    "    for j in range(3, nt+1)\n",
    "        n = mod1(j-2, 3)\n",
    "        n1 = mod1(j-1, 3)\n",
    "        n2 = mod1(j, 3)\n",
    "        \n",
    "        T[1, n2] = c1*T[1, n] + c2*T[2, n1]\n",
    "        for i in range(2, nx-2)\n",
    "            T[i, n2] = c1*T[i, n] + c2*(T[i+1, n1] + T[i-1, n1])\n",
    "        end\n",
    "        T[end,n2] = c1*T[end, n] + c2*T[end-1, n1]\n",
    "    end\n",
    "    return T[:, mod1(nt+1, 3)]\n",
    "end\n",
    "\n",
    "\n",
    "function heat_ieuler(initial_T::Function, Δx::Float64, Δt::Float64, nt::Int, L, K, C, ρ)\n",
    "    nx = trunc(Int, L/Δx)\n",
    "    T = [initial_temperature(x, L) for x in range(Δx, step=Δx, length=nx-1)]\n",
    "    \n",
    "    a = (Δt*K)/(C*ρ*Δx^2)\n",
    "    \n",
    "    # Save w and b vectors from the first step\n",
    "    \n",
    "    b = [1 + 2*a for _ ∈ 1:nx-1]\n",
    "    w = zeros(nx-2)\n",
    "    d = copy(T)\n",
    "    \n",
    "    for j ∈ 1:nx-2\n",
    "        w[j] = -a/b[j]\n",
    "        b[j+1] += w[j]*a\n",
    "        d[j+1] -= w[j]*d[j]\n",
    "    end\n",
    "    \n",
    "    T[end]=d[end]/b[end]\n",
    "    for j ∈ reverse(1:nx-2)\n",
    "        T[j] = (d[j] + a*T[j+1])/b[j]\n",
    "    end\n",
    "    \n",
    "    # Second step and onwards only compute d\n",
    "    for step ∈ 2:nt\n",
    "        d = copy(T)\n",
    "        for j ∈ 1:nx-2\n",
    "            d[j+1] -= w[j]*d[j]\n",
    "        end\n",
    "        \n",
    "        T[end]=d[end]/b[end]\n",
    "        for j ∈ reverse(1:nx-2)\n",
    "            T[j] = (d[j] + a*T[j+1])/b[j]\n",
    "        end\n",
    "    end\n",
    "    return T\n",
    "end\n",
    "\n",
    "\n",
    "function heat_cn(initial_T::Function, Δx::Float64, Δt::Float64, nt::Int, L, K, C, ρ)\n",
    "    nx = trunc(Int, L/Δx)\n",
    "    T = [initial_temperature(x, L) for x in range(Δx, step=Δx, length=nx-1)]\n",
    "    \n",
    "    a = (Δt*K)/(2. *C*ρ*Δx^2)\n",
    "    \n",
    "    diagonal = [1. - 2. *a for _ ∈ 1:nx-1]\n",
    "    u_diagonal = [a for _ ∈ 1:nx-2]\n",
    "    B_matrix = Tridiagonal(u_diagonal, diagonal, u_diagonal)\n",
    "    \n",
    "    # Save w and b vectors from the first step\n",
    "    \n",
    "    b = [1. + 2. *a for _ ∈ 1:nx-1]\n",
    "    w = zeros(nx-2)\n",
    "    d = B_matrix*T\n",
    "    \n",
    "    for j ∈ 1:nx-2\n",
    "        w[j] = -a/b[j]\n",
    "        b[j+1] += w[j]*a\n",
    "        d[j+1] -= w[j]*d[j]\n",
    "    end\n",
    "    \n",
    "    T[end]=d[end]/b[end]\n",
    "    for j ∈ reverse(1:nx-2)\n",
    "        T[j] = (d[j] + a*T[j+1])/b[j]\n",
    "    end\n",
    "    \n",
    "    # Second step and onwards only compute d\n",
    "    for step ∈ 2:nt\n",
    "        d = B_matrix*T\n",
    "        for j ∈ 1:nx-2\n",
    "            d[j+1] -= w[j]*d[j]\n",
    "        end\n",
    "        \n",
    "        T[end]=d[end]/b[end]\n",
    "        for j ∈ reverse(1:nx-2)\n",
    "            T[j] = (d[j] + a*T[j+1])/b[j]\n",
    "        end\n",
    "    end\n",
    "    return T\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "90bb2d05",
   "metadata": {},
   "source": [
    "# 2. Diffusion\n",
    "\n",
    "We attempt to integrate numerically the equation\n",
    "\\begin{equation}\n",
    "    \\frac{\\partial T(x, t)}{\\partial t} = \\frac{K}{C \\rho} \\frac{\\partial^{2}T}{\\partial x^{2}},\n",
    "\\end{equation}\n",
    "\n",
    "\n",
    "# a) FTCS\n",
    "\n",
    "The Forward Time Centered Space scheme (FTCS), as indicated by the name, uses a Forward Difference for time derivatives together with an Euler method (setting the function at time $t_{n}$) and a Central Finite Difference for spatial derivatives. Thus, for the diffusion equation we have\n",
    "\n",
    "\\begin{equation}\n",
    "    \\frac{\\partial T}{\\partial t} = F(t, x, \\partial_{x}^{2}T) \\implies \\frac{\\partial T}{\\partial t} \\overset{\\text{F.D.}}{\\approx} \\frac{T(t_{n + 1}, x_{j}) - T(t_{n}, x_{j})}{\\Delta t} \\qquad \\frac{T(t_{n + 1}, x_{j}) - T(t_{n}, x_{j})}{\\Delta t} = F\\left(t_{n}, x_{i}, \\partial_{x}^{2}T(t_{n}, x_{i})\\right) \\quad \\text{Euler method for time.}\n",
    "\\end{equation}\n",
    "\n",
    "Then, for space, we use the given form of $F(t, x, \\partial_{x}^{2}T)$ to obtain\n",
    "\n",
    "\\begin{equation}\n",
    "    \\frac{\\partial^{2}T}{\\partial x^{2}}(t_{n}, x_{j}) \\overset{\\text{C.F.D.}}{\\approx} \\frac{T(t_{n}, x_{j + 1}) - 2 T(t_{n}, x_{j}) + T(t_{n}, x_{j - 1})}{(\\Delta x)^{2}}\n",
    "\\end{equation}\n",
    "\n",
    "Hence, the equation discretized with a FTCS scheme reads\n",
    "\n",
    "\\begin{equation}\n",
    "    \\boxed{\\frac{T(t_{n + 1}, x_{j}) - T(t_{n}, x_{j})}{\\Delta t} = \\frac{K}{C \\rho} \\frac{T(t_{n}, x_{j + 1}) - 2 T(t_{n}, x_{j}) + T(t_{n}, x_{j - 1})}{(\\Delta x)^{2}}.}\n",
    "\\end{equation}"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "e58042e3",
   "metadata": {},
   "source": [
    "## Generate data"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "824c1e1e",
   "metadata": {},
   "outputs": [],
   "source": [
    "Δx = 0.01  # Spacing of the grid\n",
    "Δt = 0.1  # Time spacing\n",
    "nt = 10000  # Number of steps *after* the initial time.\n",
    "\n",
    "L = 1.  # Length of the rod\n",
    "K = 210.  # Thermal conductivity\n",
    "C = 900.  # Specific heat capacity\n",
    "ρ = 2700.  # Density\n",
    "\n",
    "initial_temperature(x, L) = sin(π*x/L)\n",
    "\n",
    "temperatures = heat_ftcs(initial_temperature, Δx, Δt, nt, L, K, C, ρ);\n",
    "\n",
    "# The next steps are just for ploting the data\n",
    "\n",
    "temperatures = transpose(temperatures)  # Needed to plot the time in the vertical axis\n",
    "boundary_points = [0. for _ in range(1, size(temperatures)[1])]\n",
    "temperatures = [boundary_points temperatures boundary_points];  # Add the boundary x=0 for aesthetics.\n",
    "x = range(0, L, step=Δx);"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "577f2aed",
   "metadata": {},
   "source": [
    "## Heatmap"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f2af5cf2",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Must generate data first\n",
    "\n",
    "t = range(0, step=Δt, length=nt+1)\n",
    "temperature_heatmap = heatmap(x, t, temperatures, xlabel=\"\\$ x \\$\", ylabel=\"\\$ t\\$\", \n",
    "                              colorbar_title=\"\\$T \\\\;(^{\\\\circ}{C})\\$\", dpi=600, labelfontsize=13)\n",
    "\n",
    "# savefig(temperature_heatmap, \"temperature_heatmap.png\")"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "e5cfc946",
   "metadata": {},
   "source": [
    "## Animation"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "e0ce717a",
   "metadata": {},
   "outputs": [],
   "source": [
    "@userplot DiffusionPlot\n",
    "@recipe function heat(diff::DiffusionPlot)\n",
    "    x, bar_height, bar_temperature, time = diff.args\n",
    "    st --> :heatmap\n",
    "    xlabel --> \"\\$ x \\$\"\n",
    "    ylabel --> \"\\$T \\\\;(^{\\\\circ}{C})\\$\"\n",
    "    title --> \"\\$ t = $time \\$\"\n",
    "    colorbar_title --> \"\\$T \\\\;(^{\\\\circ}{C})\\$\"\n",
    "    ylims --> (0, 1.4)\n",
    "    yticks --> range(0, 1., step = 0.1)\n",
    "    clims --> (0, 1.)  # (min_temp, max_temp)\n",
    "    labelfontsize --> 13 \n",
    "    dpi --> 500\n",
    "    x, bar_height, bar_temperature\n",
    "end\n",
    "\n",
    "nt, nx = size(temperatures)\n",
    "bar_height = range(1.05, 1.3, step=0.01)\n",
    "\n",
    "fps = 30\n",
    "seconds = 5\n",
    "frames=seconds*fps  # 5 seconds at 50 frames per second\n",
    "skip = trunc(Int, nt/frames)\n",
    "\n",
    "anim = @animate for j ∈ range(stop=nt, step=skip, length=frames)\n",
    "    bar_temperature = [temperatures[j, i] for _ in bar_height, i in range(1, nx)]\n",
    "    time = round(Int, (j-1)*Δt)\n",
    "    diffusionplot(x, bar_height, bar_temperature, time)\n",
    "    plot!(x, temperatures[j,:], label=nothing)\n",
    "end\n",
    "\n",
    "anim\n",
    "# gif(anim, \"Metallic_rod_$fps.gif\", fps=fps);"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "cab60f1f",
   "metadata": {},
   "source": [
    "# b) Error estimation for the FTCS scheme"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "312efc92",
   "metadata": {
    "scrolled": false
   },
   "outputs": [],
   "source": [
    "Δx = 0.01  # Spacing of the grid\n",
    "L = 1.  # Length of the rod\n",
    "K = 210.  # Thermal conductivity\n",
    "C = 900.  # Specific heat capacity\n",
    "ρ = 2700.  # Density\n",
    "\n",
    "initial_temperature(x, L) = sin(π*x/L)\n",
    "\n",
    "t_final = 100.\n",
    "method = heat_ftcs\n",
    "Δt_values = range(1e-3, 0.6, step=1e-3)\n",
    "\n",
    "T_initial = [sin(π*x/L) for x in range(Δx, step=Δx, length=trunc(Int, L/Δx)-1)]\n",
    "\n",
    "T_exact(Δt) = exp(-(π^2)*K*Δt*round(Int, t_final /Δt)/(C*ρ))*T_initial\n",
    "T_computed(method, Δt) = method(initial_temperature, Δx, Δt, round(Int, t_final /Δt), L, K, C, ρ)[:,end]\n",
    "\n",
    "ϵ_values = [sum(abs.(T_computed(Δt) - T_exact(Δt)))/100 for Δt in Δt_values];"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "681bacd5",
   "metadata": {},
   "outputs": [],
   "source": [
    "time = round(Int, t_final)\n",
    "error_b = plot(Δt_values, ϵ_values, xlabel=\"\\$ \\\\Delta t\\$\", ylabel=\"\\$ \\\\epsilon(t = $time)\\$\", \n",
    "    label=false, lw=1.5, color=:blue, legend=:bottomright, legendfontsize=12, labelfontsize=12, dpi=500)\n",
    "vline!([C*ρ*(Δx^2)/(6*K)], label=\"\\$ C \\\\rho (\\\\Delta x)^{2}/(6 K)\\$\", lw=1.6, color=:violet)\n",
    "vline!([C*ρ*(Δx^2)/(2*K)], label=\"\\$ C \\\\rho (\\\\Delta x)^{2}/(2 K)\\$\", lw=1.6, color=:red)\n",
    "\n",
    "# savefig(error_b, \"diffusion_b.png\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 33,
   "id": "402e1ddc",
   "metadata": {},
   "outputs": [],
   "source": [
    "Δx = 0.01  # Spacing of the grid\n",
    "L = 1.  # Length of the rod\n",
    "K = 210.  # Thermal conductivity\n",
    "C = 900.  # Specific heat capacity\n",
    "ρ = 2700.  # Density\n",
    "\n",
    "initial_temperature(x, L) = sin(π*x/L)\n",
    "\n",
    "t_final = 1000.\n",
    "method = heat_ftcs\n",
    "Δt_values = range(1e-3, 0.7, step=1e-3)\n",
    "\n",
    "T_initial = [sin(π*x/L) for x in range(Δx, step=Δx, length=trunc(Int, L/Δx)-1)]\n",
    "\n",
    "T_exact2(Δt) = exp(-(π^2)*K*Δt*trunc(Int, t_final /Δt)/(C*ρ))*T_initial\n",
    "T_computed2(method, Δt) = method(initial_temperature, Δx, Δt, trunc(Int, t_final /Δt), L, K, C, ρ)[:,end]\n",
    "\n",
    "ϵ_values = [sum(abs.(T_computed2(method, Δt) - T_exact2(Δt)))/100 for Δt in Δt_values]\n",
    "\n",
    "time = round(Int, t_final)\n",
    "error_b2 = plot(Δt_values, log.(ϵ_values), xlabel=\"\\$ \\\\Delta t\\$\", ylabel=\"\\$ \\\\ln[\\\\epsilon(t = $time)]\\$\", \n",
    "    label=false, lw=1.5, color=:blue, legend=:top, legendfontsize=12, labelfontsize=12, dpi=500)\n",
    "vline!([C*ρ*(Δx^2)/(6*K)], label=\"\\$ C \\\\rho (\\\\Delta x)^{2}/(6 K)\\$\", lw=1.6, color=:violet)\n",
    "vline!([C*ρ*(Δx^2.)/(2*K)], label=\"\\$ C \\\\rho (\\\\Delta x)^{2}/(2 K)\\$\", lw=1.6, color=:red)\n",
    "# savefig(error_b2, \"diffusion_b2.png\")"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "53a33ae7",
   "metadata": {},
   "source": [
    "# c) Error estimation for: Implicit Backward Euler, Crank-Nicolson and Dufort-Frankel algorithms"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 110,
   "id": "814c3b34",
   "metadata": {},
   "outputs": [],
   "source": [
    "Δx = 0.01  # Spacing of the grid\n",
    "L = 1.  # Length of the rod\n",
    "K = 210.  # Thermal conductivity\n",
    "C = 900.  # Specific heat capacity\n",
    "ρ = 2700.  # Density\n",
    "\n",
    "initial_temperature(x, L) = sin(π*x/L)\n",
    "\n",
    "t_final = 100.\n",
    "\n",
    "method1 = heat_ieuler\n",
    "method2 = heat_cn\n",
    "method3 = heat_dufofra\n",
    "method4 = heat_ftcs\n",
    "\n",
    "Δt_values = range(1e-3, 0.7, step=1e-3)\n",
    "Δt_values2 = range(1e-3, 0.6, step=1e-3)\n",
    "\n",
    "\n",
    "T_initial = [sin(π*x/L) for x in range(Δx, step=Δx, length=trunc(Int, L/Δx)-1)]\n",
    "\n",
    "T_exact(Δt) = exp(-(π^2)*K*Δt*trunc(Int, t_final /Δt)/(C*ρ))*T_initial\n",
    "T_computed(method, Δt) = method(initial_temperature, Δx, Δt, trunc(Int, t_final /Δt), L, K, C, ρ)\n",
    "\n",
    "ϵ_values1 = [sum(abs.(T_computed(method1, Δt) - T_exact(Δt)))/100 for Δt in Δt_values]\n",
    "ϵ_values2 = [sum(abs.(T_computed(method2, Δt) - T_exact(Δt)))/100 for Δt in Δt_values]\n",
    "ϵ_values3 = [sum(abs.(T_computed(method3, Δt) - T_exact(Δt)))/100 for Δt in Δt_values]\n",
    "ϵ_values4 = [sum(abs.(T_computed(method4, Δt)[:,end] - T_exact(Δt)))/100 for Δt in Δt_values2]\n",
    "\n",
    "\n",
    "\n",
    "factor1 = ((1/Δx)^2 - (π^2 /6. * L^2))* (K/(C*ρ))^2\n",
    "factor2 = (Δx)^2 / 12\n",
    "\n",
    "Δt_change = sqrt(factor2/factor1)\n",
    "\n",
    "time = round(Int, t_final);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 131,
   "id": "f4814c4a",
   "metadata": {},
   "outputs": [],
   "source": [
    "error_c = plot(Δt_values, ϵ_values3, xlabel=\"\\$ \\\\Delta t\\$\", ylabel=\"\\$ \\\\epsilon(t = $time) \\$\", \n",
    "    label=\"Du Fort-Frankel\", lw=1.5, color=:blue, legend=:topleft, legendfontsize=12, labelfontsize=12, dpi=500)\n",
    "plot!(Δt_values, ϵ_values1, color=:blue4, label=\"BTCS\", lw=1.5)\n",
    "plot!(Δt_values, ϵ_values2, color=:green3, label=\"Crank-Nicolson\", lw=1.5)\n",
    "plot!(Δt_values2, ϵ_values4, color=:orange, label=\"FTCS\", lw=1.5, size=(800,500))\n",
    "\n",
    "vline!([Δt_change], label=\"\\$ \\\\Delta t_{change}\\$\", lw=1.6, color=:violet)\n",
    "vline!([C*ρ*(Δx^2)/(6*K)], label=\"\\$ C \\\\rho (\\\\Delta x)^{2}/(6 K)\\$\", lw=1.6, color=:darkred)\n",
    "vline!([C*ρ*(Δx^2.)/(2*K)], label=\"\\$ C \\\\rho (\\\\Delta x)^{2}/(2 K)\\$\", lw=1.6, color=:red)\n",
    "# savefig(error_c, \"diffusion_c.png\")"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.8.1",
   "language": "julia",
   "name": "julia-1.8"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.8.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
